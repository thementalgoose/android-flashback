import com.github.triplet.gradle.androidpublisher.ResolutionStrategy

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'koin'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'de.mannodermaus.android-junit5'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "tmg.flashback"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 29
        versionName "5.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE") ?: "flashback.keystore")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEYSTORE_ALIAS")
            keyPassword System.getenv("KEYSTORE_PASSWORD")
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            useProguard false
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    flavorDimensions 'version'
    productFlavors {
        sand {
            dimension 'version'
            applicationIdSuffix '.sand'
            versionNameSuffix '-sand'

            resValue "string", "application_id", "tmg.flashback.sand"
            resValue "bool", "toggle_rss", "true"
            buildConfigField "int", "ENVIRONMENT", '1'
        }

        live {
            dimension 'version'

            resValue "string", "application_id", "tmg.flashback"
            resValue "bool", "toggle_rss", "true"
            buildConfigField "int", "ENVIRONMENT", '0'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        androidTest.resources.srcDirs += 'src/androidTest/res'
        test.java.srcDirs += 'src/test/java'
    }

    testOptions {
        junitPlatform {
            testLogging {
                events "skipped", "failed", "standardOut", "standardError"
            }
        }
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
            all {
                afterSuite { desc, result ->
                    if (!desc.parent) {
                        println "\n======================================================================="
                        println "${desc.displayName}"
                        println "Test result: ${result.resultType}"
                        println "Test summary: ${result.testCount} tests, " +
                                "${result.successfulTestCount} succeeded, " +
                                "${result.failedTestCount} failed, " +
                                "${result.skippedTestCount} skipped"
                        println "======================================================================="
                    }
                }
                useJUnitPlatform()
                testLogging {
                    showStandardStreams = true
                    events "skipped", "failed", "standardOut", "standardError"
                }
            }
        }
    }
}

play {
    track = 'internal'
    serviceAccountCredentials = file(System.getenv("PRIVATE_KEY") ?: "flashback.json")
    defaultToAppBundles = true
    resolutionStrategy = ResolutionStrategy.IGNORE
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat 'full'
        showCauses true
        showExceptions true
        showStackTraces true
        showStandardStreams true
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation project(path: ':repo')
    implementation project(path: ':firebase')
    implementation project(path: ':rss')
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junit"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines"
    testImplementation 'androidx.test:core:1.3.0'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation 'org.mockito:mockito-core:3.0.0'
    kaptTest "androidx.lifecycle:lifecycle-compiler:2.2.0"

    // Swipe refresh
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swipe_refresh"

    // ThreeTen
    implementation "com.jakewharton.threetenabp:threetenabp:$threeten"
    testImplementation ("org.threeten:threetenbp:$threetenbp") {
        exclude group:'com.jakewharton.threetenabp', module:'threetenabp'
    }

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$android_lifecycle"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$android_lifecycle"

    // Material components
    implementation "com.google.android.material:material:$android_material"

    // ConstraintLayout
    implementation "androidx.constraintlayout:constraintlayout:$android_constraint"

    // Koin
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-android-scope:$koin_version"
    implementation "org.koin:koin-android-viewmodel:$koin_version"
    implementation "org.koin:koin-android-ext:$koin_version"

    // Progress bar
    implementation 'com.aswin:CustomArc:1.0.2'

    // Fast indicator
    implementation 'com.reddit:indicator-fast-scroll:1.3.0'

    // TMG dependencies
    implementation "com.github.thementalgoose:android-components:$tmg_components"
    implementation "com.github.thementalgoose:android-utilities:$tmg_utils"
    implementation 'com.github.thementalgoose:android-labelled-progress-bar:1.0.3'

    // Preferences
    implementation "androidx.preference:preference-ktx:1.1.1"

    // Flagkit
    implementation 'com.jwang123.flagkit:flagkit:1.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    // BugShaker
    implementation 'com.github.stkent:bugshaker:1.4.1'

    // Indicator scroller
    implementation 'com.reddit:indicator-fast-scroll:1.3.0'

    // Inbox
    implementation 'me.saket:inboxrecyclerview:2.0.0-beta3'

    // Lottie
    implementation "com.airbnb.android:lottie:$lottie"

    // Skeleton Layout
    implementation 'com.faltenreich:skeletonlayout:2.0.1'

}

apply plugin: 'com.google.gms.google-services'