import com.github.triplet.gradle.androidpublisher.ResolutionStrategy

apply plugin: 'com.android.application'
apply from: rootDir.absolutePath + "/gradle/config-android.gradle"
apply from: rootDir.absolutePath + "/gradle/config-util.gradle"
apply from: rootDir.absolutePath + "/gradle/config-junit5.gradle"
apply from: rootDir.absolutePath + "/gradle/config-compose.gradle"
apply from: rootDir.absolutePath + "/gradle/config-koin.gradle"

apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'de.mannodermaus.android-junit5'
apply plugin: 'com.github.triplet.play'

apply from: rootDir.absolutePath + "/gradle/script-remoteconfig.gradle"
apply from: rootDir.absolutePath + "/gradle/script-releasenotes.gradle"

android {
    compileSdkVersion project.ext.compileSdkVersion

    defaultConfig {
        applicationId "tmg.flashback"
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode project.ext.versionCode // generatedVersionCode
        versionName project.ext.versionName // generatedVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument "runnerBuilder", "de.mannodermaus.junit5.AndroidJUnit5Builder"
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE") ?: "flashback.keystore")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEYSTORE_ALIAS")
            keyPassword System.getenv("KEYSTORE_PASSWORD")
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude "win32-x86-64/attach_hotspot_windows.dll"
        exclude "win32-x86/attach_hotspot_windows.dll"
        exclude "META-INF/LGPL2.1"
        exclude "META-INF/AL2.0"
        exclude "META-INF/licenses/ASM"
        exclude "META-INF/*"
    }

    flavorDimensions 'version'
    productFlavors {
        sand {
            dimension 'version'
            applicationIdSuffix '.sand'
            versionNameSuffix '-sand'

            buildConfigField "int", "ENVIRONMENT", '1'
            buildConfigField "String", "BASE_URL", '"' + project.ext.baseUrl + '"'

            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }

        live {
            dimension 'version'

            buildConfigField "int", "ENVIRONMENT", '0'
            buildConfigField "String", "BASE_URL", '"' + project.ext.baseUrl + '"'

            firebaseCrashlytics {
                mappingFileUploadEnabled true
            }
        }
    }
}

play {
    track = 'internal'
    serviceAccountCredentials = file(System.getenv("PRIVATE_KEY") ?: "flashback.json")
    defaultToAppBundles = true
    resolutionStrategy = ResolutionStrategy.IGNORE
}

// Adds generate tasks to assemble
tasks.whenTaskAdded { task ->
    if (task.name.contains("assemble")) {
        task.dependsOn generateRemoteConfigDefaults
    }
    if (task.name.contains("assemble")) {
        task.dependsOn generateReleaseNotes
    }
}

dependencies {

    // Feature modules
    implementation project(path: ":statistics")
    implementation project(path: ':data:repo')
    implementation project(path: ':data:network')
    implementation project(path: ":formula1")
    implementation project(path: ":rss")
    implementation project(path: ":common")
    implementation project(path: ":regulations")

    // Debug module
    debugImplementation project(path: ":debug")

    // Core modules
    implementation project(path: ":ads")
    implementation project(path: ":core:analytics")
    implementation project(path: ":core:appshortcuts")
    implementation project(path: ":core:configuration")
    implementation project(path: ":core:crash-reporting")
    implementation project(path: ":core:device")
    implementation project(path: ":core:notifications")
    implementation project(path: ":core:preferences")

    implementation project(path: ":presentation:ui")
    implementation project(path: ":presentation:style")

    // Android Test
    androidTestImplementation "androidx.test:rules:1.4.0"
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.3"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "org.junit.jupiter:junit-jupiter-api:$junit"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    androidTestImplementation "de.mannodermaus.junit5:android-test-core:1.2.2"
    androidTestRuntimeOnly "de.mannodermaus.junit5:android-test-runner:1.2.2"
    androidTestImplementation "org.junit.jupiter:junit-jupiter-api:$junit"
    androidTestImplementation "org.junit.jupiter:junit-jupiter-engine:$junit"
    androidTestImplementation "org.junit.jupiter:junit-jupiter-params:$junit"
    androidTestRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit"
    androidTestImplementation "io.mockk:mockk-android:$mockk"
    androidTestImplementation "io.insert-koin:koin-test:$koin_version"

    // Work Manager
    implementation deps.androidx.workmanager
    implementation deps.androidx.workmanager_ktx

    // Splash screen
    implementation deps.androidx.splashscreen

    // Discord Overlapping panels
    implementation deps.discord

    // TMG dependencies
    implementation deps.tmg.aboutthisapp
    implementation deps.tmg.utilities
    implementation deps.tmg.labelledprogressbar

    // Preferences
    implementation deps.androidx.preference

    // Glide
    implementation deps.glide.glide
    kapt deps.glide.kapt

    // BugShaker
    implementation deps.falcon
    implementation deps.shakey

    // Lottie
    implementation deps.lottie

    // Skeleton Layout
    implementation deps.skeletonlayout

    // Circle Image View
    implementation deps.circleimageview

    // Notifications + Config
    implementation platform(deps.firebase.bom)
    implementation deps.firebase.auth
    implementation deps.firebase.analytics
    implementation deps.firebase.common
    implementation deps.firebase.config
    implementation deps.firebase.crashlytics
    implementation deps.firebase.firestore
    implementation deps.firebase.firestore_ktx
    implementation deps.firebase.messaging
    implementation deps.kotlin.playservices

    // Test Utils
    testImplementation project(':test:models')
    debugImplementation project(path: ':test:providers')
    releaseImplementation project(path: ':test:providers-noop')
}

apply plugin: 'com.google.gms.google-services'
