import com.github.triplet.gradle.androidpublisher.ResolutionStrategy

apply plugin: 'com.android.application'
apply plugin: getNewRelicPlugin()

apply from: rootDir.absolutePath + "/gradle/config-android.gradle"
apply from: rootDir.absolutePath + "/gradle/config-util.gradle"
apply from: rootDir.absolutePath + "/gradle/config-junit5.gradle"
apply from: rootDir.absolutePath + "/gradle/config-compose.gradle"
apply from: rootDir.absolutePath + "/gradle/config-compose-providers.gradle"
apply from: rootDir.absolutePath + "/gradle/config-hilt.gradle"

apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'de.mannodermaus.android-junit5'
apply plugin: 'com.github.triplet.play'
apply plugin: 'com.google.firebase.firebase-perf'

apply from: rootDir.absolutePath + "/gradle/script-remoteconfig.gradle"

android {
    compileSdk project.ext.compileSdk

    defaultConfig {
        testInstrumentationRunnerArguments runnerBuilder: 'de.mannodermaus.junit5.AndroidJUnit5Builder'
        applicationId "tmg.flashback"
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode project.ext.versionCode // generatedVersionCode
        versionName project.ext.versionName // generatedVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE") ?: "flashback.keystore")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEYSTORE_ALIAS")
            keyPassword System.getenv("KEYSTORE_PASSWORD")
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        jniLibs {
            excludes += ['META-INF/*']
        }
        resources {
            excludes += ['win32-x86-64/attach_hotspot_windows.dll', 'win32-x86/attach_hotspot_windows.dll', 'META-INF/LGPL2.1', 'META-INF/AL2.0', 'META-INF/licenses/ASM', 'META-INF/*']
        }
    }


    flavorDimensions += 'version'
    productFlavors {
        sand {
            isDefault.set(true)
            dimension 'version'
            applicationIdSuffix '.sand'
            versionNameSuffix '-sand'

            buildConfigField "int", "ENVIRONMENT", '1'
            buildConfigField "String", "BASE_URL", '"' + project.ext.baseUrl + '"'
            buildConfigField "String", "CONTACT_EMAIL", '"' + project.ext.contactEmail + '"'

            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }

        live {
            dimension 'version'

            buildConfigField "int", "ENVIRONMENT", '0'
            buildConfigField "String", "BASE_URL", '"' + project.ext.baseUrl + '"'
            buildConfigField "String", "CONTACT_EMAIL", '"' + project.ext.contactEmail + '"'

            firebaseCrashlytics {
                mappingFileUploadEnabled true
            }
        }
    }
    namespace 'tmg.flashback'
}

play {
    track = 'internal'
    serviceAccountCredentials = file(System.getenv("PRIVATE_KEY") ?: "flashback.json")
    defaultToAppBundles = true
    resolutionStrategy = ResolutionStrategy.IGNORE
}

// Adds generate tasks to assemble
tasks.configureEach { task ->
    if (task.name.contains("assemble")) {
        task.dependsOn generateRemoteConfigDefaults
    }
}

dependencies {

    // Feature modules
    implementation project(path: ":feature:rss:core")
    implementation project(path: ":feature:rss:contract")
    implementation project(path: ':feature:maintenance:core')
    implementation project(path: ':feature:maintenance:contract')
    implementation project(path: ':feature:privacypolicy:core')
    implementation project(path: ':feature:privacypolicy:contract')
    implementation project(path: ":feature:widgets:core")
    implementation project(path: ":feature:widgets:contract")
    implementation project(path: ":feature:drivers:contract")
    implementation project(path: ":feature:drivers:core")
    implementation project(path: ":feature:constructors:contract")
    implementation project(path: ":feature:constructors:core")
    implementation project(path: ":feature:circuits:contract")
    implementation project(path: ":feature:circuits:core")
    implementation project(path: ":feature:season:contract")
    implementation project(path: ":feature:season:core")
    implementation project(path: ":feature:search:contract")
    implementation project(path: ":feature:search:core")
    implementation project(path: ":feature:weekend:contract")
    implementation project(path: ":feature:weekend:core")
    implementation project(path: ":feature:debug:contract")
    debugImplementation project(path: ":feature:debug:core")
    releaseImplementation project(path: ":feature:debug:noop")

    // Data
    implementation project(path: ':domain:repo')
    implementation project(path: ':domain:persistence')
    implementation project(path: ':network:flashback')
    implementation project(path: ':network:flashback-news')
    implementation project(path: ":formula1")

    // Core modules
    implementation project(path: ':core:ads:ads')
    debugImplementation project(path: ":core:ads:ads-admob")
    releaseImplementation project(path: ":core:ads:ads-noop")
    implementation project(path: ":core:metrics:googleanalytics")
    implementation project(path: ":core:metrics:crashlytics")
    implementation project(path: ":core:metrics:newrelic"   )
    implementation project(path: ":core:appshortcuts")
    implementation project(path: ":core:configuration")
    implementation project(path: ":core:device")
    implementation project(path: ":core:notifications")
    implementation project(path: ":core:preferences")
    implementation project(path: ":core:webbrowser")

    implementation project(path: ":presentation:ui")
    implementation project(path: ":presentation:style")
    implementation project(path: ":presentation:strings")
    implementation project(path: ":presentation:navigation")

    implementation project(path: ":eastereggs")

    implementation libs.bundles.androidx.workmanager

    implementation libs.androidx.splashscreen

    implementation libs.tmg.aboutthisapp
    implementation libs.tmg.utils

    implementation libs.falcon
    implementation libs.shakey
    implementation libs.lottie
    implementation libs.skeletonlayout
    implementation libs.circleimageview

    // Notifications + Config
    implementation platform(libs.firebase.bom)
    implementation libs.firebase.auth
    implementation libs.firebase.analytics
    implementation libs.firebase.common
    implementation libs.firebase.config
    implementation libs.firebase.crashlytics
    implementation libs.firebase.messaging
    implementation libs.firebase.perf

    implementation libs.newrelic.agent

    // Test Utils
    testImplementation project(':test:models')
    testImplementation project(path: ":feature:debug:core")

    // Android Test
    androidTestImplementation libs.bundles.androidxtest
    androidTestImplementation libs.test.junit5.core
    androidTestRuntimeOnly libs.test.junit5.runner
    androidTestImplementation libs.junit5.api
    androidTestImplementation libs.junit5.engine
    androidTestImplementation libs.junit5.params
    androidTestRuntimeOnly libs.junit5.engine
}

apply plugin: 'com.google.gms.google-services'

def isDebug() {
    return gradle.startParameter.taskNames.any { it.toLowerCase().contains("debug") }
}

def getNewRelicPlugin() {
    if (isDebug()) {
        logger.warn("[NEWRELIC] Plugin disabled")
        return 'com.android.application'
    } else {
        logger.warn("[NEWRELIC] Plugin enabled")
        return 'newrelic'
    }
}
